program
    : decleration* EOF
    ;

decleration
    : funDecl
    | varDecl
    | statement
    ;

funDecl
    : "fun" function
    ;

function
    : IDENTIFIER "(" ( IDENTIFIER ( "," IDENTIFIER )* )? ")" block
    ;

varDecl
    : "var" IDENTIFIER ( "=" expression )? ";"
    ;

statement
    : exprStmt
    | ifStmt
    | printStmt
    | forStmt
    | returnStmt
    | whileStmt
    | block
    ;


exprStmt
    : expression ";"
    ;

ifStmt
    : "if (" expression ")" statement ( "else" statement )?
    ;

printStmt
    : "print" expression ";"
    ;

forStmt
    : "for (" ( varDecl | exprStmt | ";" )
        expression? ";"
        expression? ")" statement
    ;

returnStmt
    : "return" expression? ";"
    ;

whileStmt
    : "while (" expression ")" statement
    ;

block
    : "{" decleration "}"
    ;

expression
    : assignment
    ;

assignment
    : IDENTIFIER "=" assignment
    | logic_or
    ;

logic_or
    : logic_and ( "or" logic_and )*
    ;

logic_and
    : equality ( "and" equality )*
    ;

equality
    : comparison (( "!=" | "==") comparison)*
    ;

comparison
    : term (( ">" | ">=" | "<" | "<=" ) term)*
    ;

term
    : factor (( "+" | "-" ) factor)*
    ;

factor
    : unary (( "/" | "*" ) unary)*
    ;

unary
    : ('!' | '-') unary
    | call
    ;

call
    : primary ( "(" ( expression ( ", " expression )* )? ")" )*
    ;

primary
    : NUMBER
    | STRING
    | 'true'
    | 'false'
    | 'nil'
    | "(" expression ")"
    | IDENTIFIER
    ;
