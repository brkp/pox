program
    : decleration* EOF
    ;

decleration
    : classDecl
    | funDecl
    | varDecl
    | statement
    ;

classDecl
    : "class" IDENTIFIER ( "<" IDENTIFIER )? "{" function* "}"
    ;

funDecl
    : "fn" function
    ;

function
    : IDENTIFIER "(" ( IDENTIFIER ( "," IDENTIFIER )* )? ")" block
    ;

varDecl
    : "let" IDENTIFIER ( "=" expression )? ";"
    ;

statement
    : exprStmt
    | ifStmt
    | forStmt
    | returnStmt
    | whileStmt
    | block
    ;

exprStmt
    : expression ";"
    ;

ifStmt
    : "if (" expression ")" statement ( "else" "if" "(" expression ")" statement )* ( "else" statement )?
    ;

forStmt
    : "for (" ( varDecl | exprStmt | ";" )
        expression? ";"
        expression? ")" statement
    ;

returnStmt
    : "return" expression? ";"
    ;

whileStmt
    : "while (" expression ")" statement
    ;

block
    : "{" decleration "}"
    ;

expression
    : assignment
    ;

assignment
    : ( call "." )? IDENTIFIER "=" assignment
    | logic_or
    ;

logic_or
    : logic_and ( "or" logic_and )*
    ;

logic_and
    : equality ( "and" equality )*
    ;

equality
    : comparison (( "!=" | "==") comparison)*
    ;

comparison
    : term (( ">" | ">=" | "<" | "<=" ) term)*
    ;

term
    : factor (( "+" | "-" ) factor)*
    ;

factor
    : unary (( "/" | "*" ) unary)*
    ;

unary
    : ('!' | '-') unary
    | call
    ;

call
    : primary ( "(" ( expression ( ", " expression )* )? ")" | "." IDENTIFIER )*
    ;

primary
    : NUMBER
    | STRING
    | 'true'
    | 'false'
    | 'nil'
    | "(" expression ")"
    | IDENTIFIER
    | "super" "." IDENTIFIER;
    ;
